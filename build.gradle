plugins {
    id 'java'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '3.4.2' apply false
}

allprojects {
    group = 'ru.practicum'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
        }
    }

    configurations.configureEach {
        resolutionStrategy {
            failOnVersionConflict()
            force("commons-logging:commons-logging:1.3.5")
        }
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:$Versions.springBom")
                mavenBom("software.amazon.awssdk:bom:$Versions.awsBom")
            }
            dependency("org.postgresql:r2dbc-postgresql:${Versions.r2dbc}")
            dependency("com.opencsv:opencsv:${Versions.openCsv}")
            dependency("commons-beanutils:commons-beanutils:${Versions.beanUtils}")
            dependency("org.springdoc:springdoc-openapi-starter-webflux-ui:${Versions.openApi}")
        }
    }
}

task managedVersions {
    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}